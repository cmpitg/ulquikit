= Ulquikit Utilities
:Author: Nguyễn Hà Dương (cmpitg)
:Email: <cmpitg@gmail.com>
:toc: left
:toclevels: 4
:numbered:
:icons: font
:source-highlighter: pygments
:pygments-css: class

== General-purpose utilities

General-purpose utility functions.

.file::utils/utils.rkt
[source,racket,linenums]
----
;; include::license-header

;; include::utils/use-rackjure

(provide (all-defined-out))

(module+ test
  (require rackunit))

(define inc (partial + 1))

(define dec #λ(- % 1))

(define box-set!
  #λ(box-swap! %1 (λ (_) %2)))

(module+ test
  (check-equal? (box-set! (box 0)   1)   1)
  (check-equal? (box-set! (box "a") "b") "b"))

----


== String

.file::utils/string.rkt
[source,racket,linenums]
----
;; include::license-header

;; include::utils/use-rackjure

(require "utils.rkt")

(provide (all-defined-out))

(module+ test
  (require rackunit))

;; include::utils/string/implementation

----

.code::utils/string/implementation
[source,racket,linenums]
----
;; #lang racket

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Implementation
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define string-starts-with?
  #λ(regexp-match? (regexp (str "^" (regexp-quote %2))) %1))

(module+ test
  (check-equal? (string-starts-with? "" "") #t)
  (check-equal? (string-starts-with? "a" "") #t)
  (check-equal? (string-starts-with? "a" "a") #t)
  (check-equal? (string-starts-with? "a" "b") #f)
  (check-equal? (string-starts-with? "abcdef" "abc") #t)
  (check-equal? (string-starts-with? "abcdef" "abb") #f)
  (check-equal? (string-starts-with? "$abc" "$a") #t))

(define string-ends-with?
  #λ(regexp-match? (regexp (str (regexp-quote %2) "$")) %1))

(module+ test
  (check-equal? (string-ends-with? "" "") #t)
  (check-equal? (string-ends-with? "a" "") #t)
  (check-equal? (string-ends-with? "a" "a") #t)
  (check-equal? (string-ends-with? "a" "b") #f)
  (check-equal? (string-ends-with? "abcdef" "def") #t)
  (check-equal? (string-ends-with? "abcdef" "dee") #f)
  (check-equal? (string-ends-with? "$ab$$c" "b$$c") #t))

(define string-empty? #λ(zero? (string-length %)))

(define string-rest #λ(if (string-empty? %)
                          ""
                          (substring % 1)))

(module+ test
  (check-equal? (string-rest "") "")
  (check-equal? (string-rest "a") "")
  (check-equal? (string-rest "ab") "b")
  (check-equal? (string-rest "abc") "bc"))

----


== Path

.file::utils/path.rkt
[source,racket,linenums]
----
;; include::license-header

;; include::utils/use-rackjure

(require racket/path)

(require "string.rkt")

(provide (all-defined-out))

(module+ test
  (require rackunit))

;; include::utils/path/implementation
----

.code::utils/path/implementation
[source,racket,linenums]
----
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Implementation
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define read-file #λ(call-with-input-file % port->string))

(define standardize-path simple-form-path)

(define get-relative-path
  #λ(simple-form-path (apply build-path %&)))

(define get-temp-dir #λ(find-system-path 'temp-dir))

(define remove-dir
  #λ(delete-directory/files % #:must-exist? #f))

(define create-dir make-directory*)

(define create-empty-file
  #λ(with-output-to-file %1
      (λ () (display ""))
      #:mode 'text
      #:exists 'truncate/replace))

(module+ test
  (let ([random-file (get-relative-path (get-temp-dir)
                                        "___random-file.txt")])
    (create-empty-file random-file)
    (check-equal? (file-exists? random-file) #t)
    (check-equal? (read-file random-file) "")))

(define (list-all-adocs path)
  (with-handlers ([exn:fail? (λ (exn) '())])
    (~>> (find-files #λ(string-ends-with? % ".adoc") (expand-user-path path))
      (map path->string))))

(module+ test
  (let* ([temp-dir (get-relative-path (get-temp-dir)
                                      "./ulquikit-tmp")]
         [filenames '("hello-world.adoc"
                      "hola-mundo.adoc"
                      "mostly-harmless.adoc"
                      "42.adoc")]

         [filenames/fullpath (for/list ([name (in-list filenames)])
                               (format "~a/~a" temp-dir name))])
    (with-handlers ([exn:fail? #λ(remove-dir temp-dir)])
      (remove-dir temp-dir)
      (create-dir temp-dir)
      (for ([path (in-list filenames/fullpath)])
        (create-empty-file path))

      (check-equal? (sort (list-all-adocs temp-dir) string<?)
                    (sort filenames/fullpath string<?))
      (remove-dir temp-dir))))

(define path->directory
  #λ(if-let [path (file-name-from-path %)]
      (~>> path
        path->string
        (string-split %)
        first)
      %))

(module+ test
  (check-equal? (path->directory "/tmp/tmp.rkt") "/tmp/")
  (check-equal? (path->directory "/tmp/tmp/")    "/tmp/tmp/"))

;; include::path/get-path

----

=== Path building with +get-path+

One of the most important functions in this path libraries is +get-path+.
Since +build-path+ doesn't work with absolute path and doesn't cleanse the
path, +get-path+ is there to save the day!  Note that +get-path+ returns a
string instead of a path.  Moreover, +get-path+ automatically prefix the path
with current working directory of the path is relative.

.code::path/get-path
[source,racket,linenums]
----
(define (get-path . args)
  (let* ([paths/expanded (map expand-user-path args)]
         [paths/reversed (reverse (cons (current-directory) paths/expanded))]
         
         [paths/no-absolute (~> paths/reversed
                              (takef #λ(not (absolute-path? %)))
                              reverse)]
         [path/absolute     (~> paths/reversed
                              (dropf #λ(not (absolute-path? %)))
                              first)]
         [paths             (cons path/absolute paths/no-absolute)])
    (~> (apply build-path paths)
      simplify-path
      path->string)))

(module+ test
  (check-equal? (get-path "/tmp") "/tmp")
  (check-equal? (get-path "/tmp/") "/tmp/")
  (check-equal? (get-path "/tmp" "/hello") "/hello")
  (check-equal? (get-path "/tmp" "./hello") "/tmp/hello")
  (check-equal? (get-path "/tmp" "/hello" "~")
                (path->string (find-system-path 'home-dir)))
  (check-equal? (get-path "/tmp" "/hello" "/world" "./superman/")
                "/world/superman/")
  (check-equal? (get-path "world/")
                (path->string (build-path (current-directory) "world/"))))
----


== Misc

.code::utils/use-rackjure
[source,racket,linenums]
----
#lang rackjure

;; Using hashtable with curly-dict notation
(current-curly-dict hash)
----
