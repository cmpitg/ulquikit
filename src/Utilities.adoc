= Ulquikit Utilities
:Author: Nguyễn Hà Dương (cmpitg)
:Email: <cmpitg@gmail.com>
:toc: left
:toclevels: 4
:numbered:
:icons: font
:source-highlighter: pygments
:pygments-css: class

== General-purpose utilities

General-purpose utility functions.

.file::utils/utils.rkt
[source,racket,linenums]
----
;; include::license-header

;; include::utils/use-rackjure

(provide (all-defined-out))

(define inc (partial + 1))
(define dec #λ(- % 1))

----


== String

.file::utils/string.rkt
[source,racket,linenums]
----
;; include::license-header

;; include::utils/use-rackjure

(require srfi/1)

(require "utils.rkt")

(module+ test
  (require rackunit))

(provide (all-defined-out))

;; include::utils/string/implementation

----

.code::utils/string/implementation
[source,racket,linenums]
----
;; #lang racket

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Implementation
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (trim str)
  (let* ([str/list                  (string->list str)]
         [str/list/trim-begin       (drop-while #λ(equal? % #\space)
                                                str/list)]
         [str/list/reversed         (reverse str/list/trim-begin)]
         [str/list/reversed/trimmed (drop-while #λ(equal? % #\space)
                                                str/list/reversed)])
    (~> str/list/reversed/trimmed
      reverse
      list->string)))

(module+ test
  (check-equal? (trim "     ") "")
  (check-equal? (trim "  -  ") "-"))

(define string-starts-with?
  #λ(regexp-match? (regexp (str "^" (regexp-quote %2))) %1))

(module+ test
  (check-equal? (string-starts-with? "" "") #t)
  (check-equal? (string-starts-with? "a" "") #t)
  (check-equal? (string-starts-with? "a" "a") #t)
  (check-equal? (string-starts-with? "a" "b") #f)
  (check-equal? (string-starts-with? "abcdef" "abc") #t)
  (check-equal? (string-starts-with? "abcdef" "abb") #f)
  (check-equal? (string-starts-with? "$abc" "$a") #t))

(define string-ends-with?
  #λ(regexp-match? (regexp (str (regexp-quote %2) "$")) %1))

(module+ test
  (check-equal? (string-ends-with? "" "") #t)
  (check-equal? (string-ends-with? "a" "") #t)
  (check-equal? (string-ends-with? "a" "a") #t)
  (check-equal? (string-ends-with? "a" "b") #f)
  (check-equal? (string-ends-with? "abcdef" "def") #t)
  (check-equal? (string-ends-with? "abcdef" "dee") #f)
  (check-equal? (string-ends-with? "$ab$$c" "b$$c") #t))

(define string-rest #λ(~> (string->list %)
                        ((λ (lst)
                           (if (empty? lst)
                               lst
                               (rest lst))))
                        list->string))

(module+ test
  (check-equal? (string-rest "") "")
  (check-equal? (string-rest "a") "")
  (check-equal? (string-rest "ab") "b")
  (check-equal? (string-rest "abc") "bc"))

----


== Path

.file::utils/path.rkt
[source,racket,linenums]
----
;; include::license-header

;; include::utils/use-rackjure

(require racket/path)
(require "string.rkt")

(provide (all-defined-out))

(module+ test
  (require rackunit))

;; include::utils/path/implementation
----

.code::utils/path/implementation
[source,racket,linenums]
----
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Implementation
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define read-file #λ(call-with-input-file % port->string))

(define standardize-path simple-form-path)

(define get-relative-path
  #λ(simple-form-path (apply build-path %&)))

(define get-temp-dir #λ(find-system-path 'temp-dir))

(define remove-dir
  #λ(delete-directory/files % #:must-exist? #f))

(define create-dir make-directory*)

(define create-empty-file
  #λ(with-output-to-file %1
      (λ () (display ""))
      #:mode 'text
      #:exists 'truncate/replace))

(module+ test
  (let ([random-file (get-relative-path (get-temp-dir)
                                        "___random-file.txt")])
    (create-empty-file random-file)
    (check-equal? (file-exists? random-file) #t)
    (check-equal? (read-file random-file) "")))

(define (list-all-adocs path)
  (with-handlers ([exn:fail? (λ (exn) '())])
    (~>> (find-files #λ(string-ends-with? % ".adoc") (expand-user-path path))
      (map path->string))))

(module+ test
  (let* ([temp-dir (get-relative-path (get-temp-dir)
                                      "./ulquikit-tmp")]
         [filenames '("hello-world.adoc"
                      "hola-mundo.adoc"
                      "mostly-harmless.adoc"
                      "42.adoc")]

         [filenames/fullpath (for/list ([name (in-list filenames)])
                               (format "~a/~a" temp-dir name))])
    (with-handlers ([exn:fail? #λ(remove-dir temp-dir)])
      (remove-dir temp-dir)
      (create-dir temp-dir)
      (for ([path (in-list filenames/fullpath)])
        (create-empty-file path))

      (check-equal? (sort (list-all-adocs temp-dir) string<?)
                    (sort filenames/fullpath string<?))
      (remove-dir temp-dir))))

(define path->directory
  #λ(if-let [path (file-name-from-path %)]
      (~>> path
        path->string
        (string-split %)
        first)
      %))

(module+ test
  (check-equal? (path->directory "/tmp/tmp.rkt") "/tmp/")
  (check-equal? (path->directory "/tmp/tmp/")    "/tmp/tmp/"))

----

== Text

This module is born out of the need for a better text processing method,
starting with the problem of breaking a string into lines.

To break (or rather split) a string into lines, the first, obvious way came to
my head was to use +string-split+ function with +\n+ as the separator.  Let's
have a look:

[source,racket,linenums]
----
(string-split "abc" "\n")
;; ⇨ ("abc")

(string-split "abc\ndef" "\n")
;; ⇨ ("abc" "def")

(string-split "abc\ndef\n" "\n")
;; ⇨ ("abc" "def")
;; This is actually fine

(string-split "\nabc\ndef\n" "\n")
;; ⇨ ("abc" "def")
;; Oops, now we got a problem
----

The last 2 examples show that +string-split+ breaks the structure of the input
string, i.e. blank lines are not preversed.  This is a problem when the
content of a file needs to be broken into lines while line numbers are
preversed.

== Misc

.code::utils/use-rackjure
[source,racket,linenums]
----
#lang rackjure

;; Using hashtable with curly-dict notation
(current-curly-dict hash)
----
